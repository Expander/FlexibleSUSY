/**
\page mssmefthiggs MSSMEFTHiggs
\tableofcontents

\section model_mssmefthiggs MSSMEFTHiggs

MSSMEFTHiggs (FlexibleEFTHiggs for the MSSM) is an implementation of
the Standard Model, matched to the MSSM at the SUSY scale,
\f$M_\text{SUSY}\f$.  The setup of MSSMEFTHiggs is shown in the
following figure.

\image html MSSMEFTHiggs_tower.svg

\subsection mssmefthiggs_boundary_conditions Boundary conditions

__High scale__

In MSSMEFTHiggs, the `HighScale` variable is set to the SUSY scale,
\f$M_{\text{SUSY}}\f$.  At this scale the quartic Higgs coupling,
\f$\lambda(M_\text{SUSY})\f$, is predicted from the matching of the
Higgs pole masses of the Standard Model and the MSSM at the full
1-loop level (FlexibleEFTHiggs method): \f[(M_h^2)_{\text{SM}} =
(M_h^2)_{\text{MSSM}}\f] The Higgs pole mass in the Standard Model is
decomposed into a tree-level and 1-loop part as \f$(M_h^2)_{\text{SM}}
= \lambda v^2 + (\Delta m_h^2)_{\text{SM}}\f$ and the quartic Higgs
coupling is calculated as \f[\lambda(M_{\text{SUSY}}) =
\frac{1}{v^2}\Big[(M_h^2)_{\text{MSSM}} - (\Delta
m_h^2)_{\text{SM}}\Big]\f] This physical matching condition
incorporates the \f$O(v^2/M_\text{SUSY}^2)\f$ suppressed terms to all
orders into \f$\lambda(M_\text{SUSY})\f$.  Thus, MSSMEFTHiggs can
correctly predict the Higgs pole mass of the MSSM at the full 1-loop
level for both low and high SUSY scales.  In other words, MSSMEFTHiggs
is a hybrid calculation, which combines a fixed-order with an EFT
calculation.  See [<a
href="https://arxiv.org/abs/1609.00371">arXiv:1609.00371</a>] for a
detailed description of the FlexibleEFTHiggs method.

The 3- and partial 4-loop renormalization group equations of
[<a href="https://arxiv.org/abs/1303.4364">arXiv:1303.4364</a>,
<a href="https://arxiv.org/abs/1307.3536">arXiv:1307.3536</a>,
<a href="https://arxiv.org/abs/1508.00912">arXiv:1508.00912</a>,
<a href="https://arxiv.org/abs/1508.02680">arXiv:1508.02680</a>,
<a href="https://arxiv.org/abs/1604.00853">arXiv:1604.00853</a>]
are used to run \f$\lambda(M_\text{SUSY})\f$ down to the electroweak
scale \f$M_Z\f$ or \f$M_t\f$.

__Low scale__

The `LowScale` is set to \f$M_Z\f$.  At this scale, the
\f$\overline{\text{MS}}\f$ gauge and Yukawa couplings
\f$g_{1,2,3}(M_Z)\f$, \f$Y_{u,d,e}(M_Z)\f$, as well as the SM vacuum
expectation value (VEV), \f$v(M_Z)\f$, are calculated at the full
1-loop level from the known low-energy couplings
\f$\alpha_{\text{em}}^{\text{SM(5)}}(M_Z)\f$,
\f$\alpha_s^{\text{SM(5)}}(M_Z)\f$, from the pole masses \f$M_Z\f$,
\f$M_e\f$, \f$M_\mu\f$, \f$M_\tau\f$, \f$M_t\f$ as well as from the
\f$\overline{\text{MS}}\f$ masses \f$m_b^{\text{SM(5)}}(m_b)\f$,
\f$m_c^{\text{SM(4)}}(m_c)\f$, \f$m_s(2\,\text{GeV})\f$,
\f$m_d(2\,\text{GeV})\f$, \f$m_u(2\,\text{GeV})\f$.  In addition to
these 1-loop corrections, the known 2-loop and 3-loop QCD threshold
corrections for \f$\alpha_s(M_Z)\f$ from [<a
href="https://arxiv.org/abs/hep-ph/9305305">arXiv:hep-ph/9305305</a>,
<a
href="https://arxiv.org/abs/hep-ph/9708255">arXiv:hep-ph/9708255</a>,
<a
href="https://arxiv.org/abs/hep-ph/9707474">arXiv:hep-ph/9707474</a>,
<a
href="https://arxiv.org/abs/hep-ph/0004189">arXiv:hep-ph/0004189</a>]
can be taken into account in addition by setting the threshold
corrections flag appropriately.  In the calculation of the Standard
Model \f$\overline{\text{MS}}\f$ top Yukawa coupling, \f$y_t(M_Z)\f$,
the known 2-loop [<a
href="https://arxiv.org/abs/hep-ph/9803493">arXiv:hep-ph/9803493</a>]
and 3-loop [<a
href="https://arxiv.org/abs/hep-ph/9911434">arXiv:hep-ph/9911434</a>]
QCD corrections can be taken into account.  See Section @ref
FlexibleSUSY_config for a description of the individual flags to
enable/disable 2- and 3-loop threshold corrections in FlexibleSUSY.

__Top mass scale__

The Higgs and W boson pole masses, \f$M_h\f$ and \f$M_Z\f$ are
calculated at the scale \f$M_t\f$, which is an input parameter.
Furthermore, the electroweak symmetry breaking condition of the
Standard Model is imposed at the scale \f$M_t\f$ to fix the value of
the bililear Higgs coupling \f$\mu^2(M_t)\f$ in the Standard Model.

\subsection mssmefthiggs_pole_masses Pole masses

The Higgs and W boson pole masses, \f$M_h\f$ and \f$M_Z\f$, are
calculated at the full 1-loop level in the Standard Model, including
potential flavour mixing and momentum dependence.  Depending on the
given configuration flags, additional 2-, 3- and 4-loop corrections to
the Higgs pole mass of \f$O(\alpha_t\alpha_s)\f$ [<a
href="https://arxiv.org/abs/1407.4336">arXiv:1407.4336</a>, Eq.(2.47)]
and \f$O(\alpha_t^2)\f$ [<a
href="https://arxiv.org/abs/1205.6497">arXiv:1205.6497</a>, Eq.(20)],
as well as 3-loop corrections \f$O(\alpha_t^3 + \alpha_t^2\alpha_s +
\alpha_t\alpha_s^2)\f$ [<a
href="https://arxiv.org/abs/1407.4336">arXiv:1407.4336</a>] and 4-loop
corrections \f$O(\alpha_t\alpha_s^3)\f$ [<a
href="https://arxiv.org/abs/1508.00912">arXiv:1508.00912</a>] can be
taken into account.

@note Note, that the 3-loop contributions \f$O(\alpha_t^3 +
\alpha_t^2\alpha_s)\f$ are incomplete, because the corresponding
2-loop threshold corrections \f$O(\alpha_t^2 + \alpha_t\alpha_s)\f$ to
the running top Yukawa coupling are not implemented yet.

The MSSM particle masses are calculated at the full 1-loop level in
the MSSM at the SUSY scale \f$M_{\text{SUSY}}\f$.

\subsection mssmefthiggs_input_parameters Input parameters

MSSMEFTHiggs takes the following physics parameters as input:

 Parameter                                  | Description                                                          | SLHA block/field | Mathematica symbol
--------------------------------------------|----------------------------------------------------------------------|------------------|--------------------
\f$M_{\text{SUSY}}\f$                       | SUSY scale                                                           | `EXTPAR[0]`      | `MSUSY`
\f$M_1(M_\text{SUSY})\f$                    | Bino mass                                                            | `EXTPAR[1]`      | `M1Input`
\f$M_2(M_\text{SUSY})\f$                    | Wino mass                                                            | `EXTPAR[2]`      | `M2Input`
\f$M_3(M_\text{SUSY})\f$                    | Gluino mass                                                          | `EXTPAR[3]`      | `M3Input`
\f$\mu(M_\text{SUSY})\f$                    | \f$\mu\f$-parameter                                                  | `EXTPAR[4]`      | `MuInput`
\f$m_A(M_\text{SUSY})\f$                    | running CP-odd Higgs mass                                            | `EXTPAR[5]`      | `mAInput`
\f$\tan\beta(M_\text{SUSY})\f$              | \f$\tan\beta(M_\text{SUSY})=v_u(M_\text{SUSY})/v_d(M_\text{SUSY})\f$ | `EXTPAR[25]`     | `TanBeta`
\f$(A_u)_{ij}(M_\text{SUSY})\f$             | trililear up-type squark couplings                                   | `AUIN`           | `AuInput`
\f$(A_d)_{ij}(M_\text{SUSY})\f$             | trililear down-type squark couplings                                 | `ADIN`           | `AdInput`
\f$(A_e)_{ij}(M_\text{SUSY})\f$             | trililear down-type sfermion couplings                               | `AEIN`           | `AeInput`
\f$(m_{\tilde{q}}^2)_{ij}(M_\text{SUSY})\f$ | soft-breaking left-handed squark mass parameters                     | `MSQ2IN`         | `mq2Input`
\f$(m_{\tilde{u}}^2)_{ij}(M_\text{SUSY})\f$ | soft-breaking right-handed up-type squark mass parameters            | `MSU2IN`         | `mu2Input`
\f$(m_{\tilde{d}}^2)_{ij}(M_\text{SUSY})\f$ | soft-breaking right-handed down-type squark mass parameters          | `MSD2IN`         | `md2Input`
\f$(m_{\tilde{l}}^2)_{ij}(M_\text{SUSY})\f$ | soft-breaking left-handed slepton mass parameters                    | `MSL2IN`         | `ml2Input`
\f$(m_{\tilde{e}}^2)_{ij}(M_\text{SUSY})\f$ | soft-breaking right-handed down-type slepton mass parameters         | `MSE2IN`         | `me2Input`

The MSSM parameters are defined in the \f$\overline{\text{DR}}\f$
scheme at the scale \f$M_{\text{SUSY}}\f$.

\subsection mssmefthiggs_running Running MSSMEFTHiggs

We recommend to run MSSMEFTHiggs with the following configuration flags: In
an SLHA input file we recommend to use:

~~~~~~~~~~~~~~~~~~~~~~~{.txt}
Block FlexibleSUSY
    0   1.0e-05      # precision goal
    1   0            # max. iterations (0 = automatic)
    2   0            # algorithm (0 = all, 1 = two_scale, 2 = semi_analytic)
    3   1            # calculate SM pole masses
    4   4            # pole mass loop order
    5   4            # EWSB loop order
    6   4            # beta-functions loop order
    7   3            # threshold corrections loop order
    8   1            # Higgs 2-loop corrections O(alpha_t alpha_s)
    9   1            # Higgs 2-loop corrections O(alpha_b alpha_s)
   10   1            # Higgs 2-loop corrections O(alpha_t^2 + alpha_t alpha_b + alpha_b^2)
   11   1            # Higgs 2-loop corrections O(alpha_tau^2)
   12   0            # force output
   13   1            # Top pole mass QCD corrections (0 = 1L, 1 = 2L, 2 = 3L)
   14   1.0e-11      # beta-function zero threshold
   15   0            # calculate observables (a_muon, ...)
   16   0            # force positive majorana masses
   17   0            # pole mass renormalization scale (0 = SUSY scale)
   18   0            # pole mass renormalization scale in the EFT (0 = min(SUSY scale, Mt))
   19   0            # EFT matching scale (0 = SUSY scale)
   20   2            # EFT loop order for upwards matching
   21   1            # EFT loop order for downwards matching
   22   0            # EFT index of SM-like Higgs in the BSM model
   23   1            # calculate BSM pole masses
   24   123111321    # individual threshold correction loop orders
   25   0            # ren. scheme for Higgs 3L corrections (0 = DR, 1 = MDR)
   26   1            # Higgs 3-loop corrections O(alpha_t alpha_s^2)
   27   1            # Higgs 3-loop corrections O(alpha_b alpha_s^2)
   28   1            # Higgs 3-loop corrections O(alpha_t^2 alpha_s)
   29   1            # Higgs 3-loop corrections O(alpha_t^3)
   30   1            # Higgs 4-loop corrections O(alpha_t alpha_s^3)
~~~~~~~~~~~~~~~~~~~~~~~

In the Mathematica interface we recommend to use:

~~~~~~~~~~~~~~~~~~~~~~~{.m}
handle = FSMSSMEFTHiggsOpenHandle[
    fsSettings -> {
        precisionGoal -> 1.*^-5,           (* FlexibleSUSY[0] *)
        maxIterations -> 0,                (* FlexibleSUSY[1] *)
        solver -> 0,                       (* FlexibleSUSY[2] *)
        calculateStandardModelMasses -> 1, (* FlexibleSUSY[3] *)
        poleMassLoopOrder -> 4,            (* FlexibleSUSY[4] *)
        ewsbLoopOrder -> 4,                (* FlexibleSUSY[5] *)
        betaFunctionLoopOrder -> 4,        (* FlexibleSUSY[6] *)
        thresholdCorrectionsLoopOrder -> 3,(* FlexibleSUSY[7] *)
        higgs2loopCorrectionAtAs -> 1,     (* FlexibleSUSY[8] *)
        higgs2loopCorrectionAbAs -> 1,     (* FlexibleSUSY[9] *)
        higgs2loopCorrectionAtAt -> 1,     (* FlexibleSUSY[10] *)
        higgs2loopCorrectionAtauAtau -> 1, (* FlexibleSUSY[11] *)
        forceOutput -> 0,                  (* FlexibleSUSY[12] *)
        topPoleQCDCorrections -> 1,        (* FlexibleSUSY[13] *)
        betaZeroThreshold -> 1.*^-11,      (* FlexibleSUSY[14] *)
        forcePositiveMasses -> 0,          (* FlexibleSUSY[16] *)
        poleMassScale -> 0,                (* FlexibleSUSY[17] *)
        eftPoleMassScale -> 0,             (* FlexibleSUSY[18] *)
        eftMatchingScale -> 0,             (* FlexibleSUSY[19] *)
        eftMatchingLoopOrderUp -> 0,       (* FlexibleSUSY[20] *)
        eftMatchingLoopOrderDown -> 1,     (* FlexibleSUSY[21] *)
        eftHiggsIndex -> 0,                (* FlexibleSUSY[22] *)
        calculateBSMMasses -> 1,           (* FlexibleSUSY[23] *)
        thresholdCorrections -> 123111321, (* FlexibleSUSY[24] *)
        higgs3loopCorrectionRenScheme -> 0,(* FlexibleSUSY[25] *)
        higgs3loopCorrectionAtAsAs -> 1,   (* FlexibleSUSY[26] *)
        higgs3loopCorrectionAbAsAs -> 1,   (* FlexibleSUSY[27] *)
        higgs3loopCorrectionAtAtAs -> 1,   (* FlexibleSUSY[28] *)
        higgs3loopCorrectionAtAtAt -> 1,   (* FlexibleSUSY[29] *)
        higgs4loopCorrectionAtAsAsAs -> 1, (* FlexibleSUSY[30] *)
        parameterOutputScale -> 0          (* MODSEL[12] *)
    },
    ...
];
~~~~~~~~~~~~~~~~~~~~~~~

\subsubsection mssmefthiggs_uncertainty Uncertainty estimate of the predicted Higgs pole mass

In the file
<code>model_files/MSSMEFTHiggs/MSSMEFTHiggs_uncertainty_estimate.m</code>
FlexibleSUSY provides the Mathematica function
`CalcMSSMEFTHiggsDMh[]`, which calculates the Higgs pole mass with
MSSMEFTHiggs and performs an uncertainty estimate of missing higher
order corrections.  Two main sources of the theory uncertainty are
taken into account:

 - _SM uncertainty_: Missing higher order corrections in the
   calculation of the running Standard Model top Yukawa coupling and
   in the calculation of the Higgs pole mass.  The uncertainty from
   this source is estimated by (i) switching on/off the 3-loop QCD
   contributions in the calculation of the running top Yukawa coupling
   \f$y_t^{\text{SM}}(M_Z)\f$ from the top pole mass and by (ii)
   varying the renormalization scale at which the Higgs pole mass is
   calculated within the interval \f$[M_t/2, 2 M_t]\f$.

 - _SUSY uncertainty_: Missing higher order corrections in the
   calculation of the quartic Higgs coupling
   \f$\lambda(M_\text{SUSY})\f$.  This uncertainty is estimated by
   varying the matching scale within the interval
   \f$[M_{\text{SUSY}}/2, 2 M_{\text{SUSY}}]\f$.

The following code snippet illustrates the calculation of the Higgs
pole mass calculated at the 3-loop level with MSSMEFTHiggs as a
function of the SUSY scale (red solid line), together with the
estimated uncertainty (grey band).

~~~~~~~~~~~~~~~~~~~~~~~{.m}
Get["models/MSSMEFTHiggs/MSSMEFTHiggs_librarylink.m"];
Get["model_files/MSSMEFTHiggs/MSSMEFTHiggs_uncertainty_estimate.m"];

settings = {
    precisionGoal -> 1.*^-5,
    poleMassLoopOrder -> 3,
    ewsbLoopOrder -> 3,
    betaFunctionLoopOrder -> 3,
    thresholdCorrectionsLoopOrder -> 3,
    thresholdCorrections -> 123111321
};

smpars = {
    alphaEmMZ -> 1/127.916, (* SMINPUTS[1] *)
    GF -> 1.166378700*^-5,  (* SMINPUTS[2] *)
    alphaSMZ -> 0.1184,     (* SMINPUTS[3] *)
    MZ -> 91.1876,          (* SMINPUTS[4] *)
    mbmb -> 4.18,           (* SMINPUTS[5] *)
    Mt -> 173.34,           (* SMINPUTS[6] *)
    Mtau -> 1.77699,        (* SMINPUTS[7] *)
    Mv3 -> 0,               (* SMINPUTS[8] *)
    MW -> 80.385,           (* SMINPUTS[9] *)
    Me -> 0.000510998902,   (* SMINPUTS[11] *)
    Mv1 -> 0,               (* SMINPUTS[12] *)
    Mm -> 0.1056583715,     (* SMINPUTS[13] *)
    Mv2 -> 0,               (* SMINPUTS[14] *)
    md2GeV -> 0.00475,      (* SMINPUTS[21] *)
    mu2GeV -> 0.0024,       (* SMINPUTS[22] *)
    ms2GeV -> 0.104,        (* SMINPUTS[23] *)
    mcmc -> 1.27,           (* SMINPUTS[24] *)
    CKMTheta12 -> 0,
    CKMTheta13 -> 0,
    CKMTheta23 -> 0,
    CKMDelta -> 0,
    PMNSTheta12 -> 0,
    PMNSTheta13 -> 0,
    PMNSTheta23 -> 0,
    PMNSDelta -> 0,
    PMNSAlpha1 -> 0,
    PMNSAlpha2 -> 0,
    alphaEm0 -> 1/137.035999074,
    Mh -> 125.09
};

MSSMEFTHiggsCalcMh[MS_, TB_, Xtt_] :=
    CalcMSSMEFTHiggsDMh[
        fsSettings -> settings,
        fsSMParameters -> smpars,
        fsModelParameters -> {
            MSUSY   -> MS,
            M1Input -> MS,
            M2Input -> MS,
            M3Input -> MS,
            MuInput -> MS,
            mAInput -> MS,
            TanBeta -> TB,
            mq2Input -> MS^2 IdentityMatrix[3],
            mu2Input -> MS^2 IdentityMatrix[3],
            md2Input -> MS^2 IdentityMatrix[3],
            ml2Input -> MS^2 IdentityMatrix[3],
            me2Input -> MS^2 IdentityMatrix[3],
            AuInput -> {{MS/TB, 0    , 0},
                        {0    , MS/TB, 0},
                        {0    , 0    , MS/TB + Xtt MS}},
            AdInput -> MS TB IdentityMatrix[3],
            AeInput -> MS TB IdentityMatrix[3]
        }
   ];

LinearRange[start_, stop_, steps_] :=
    Range[start, stop, (stop - start)/steps];

Xtt = Sqrt[6];
TB  = 5;

data = ParallelMap[
    { N[#], Sequence @@ MSSMEFTHiggsCalcMh[#, TB, Xtt] }&,
    LinearRange[500, 10^4, 100]
];

MhMin[{MS_, Mh_, DMh_}]  := {MS, Mh - DMh};
MhMax[{MS_, Mh_, DMh_}]  := {MS, Mh + DMh};
MhBest[{MS_, Mh_, DMh_}] := {MS, Mh};

dataMhMin  = MhMin  /@ data;
dataMhMax  = MhMax  /@ data;
dataMhBest = MhBest /@ data;

plot2 = ListLinePlot[dataMhBest,
                     PlotStyle -> {Red, Thick}];

plot1 = ListLinePlot[{dataMhMax, dataMhMin},
                     PlotStyle -> LightGray,
                     Filling -> {1 -> {{2}, LightGray}},
                     PlotRange -> All];

plot = Show[{plot1, plot2},
            PlotLabel -> "Xt = Sqrt[6]*MS, tan\[Beta] = 5",
            PlotRange -> Automatic,
            Axes -> False, Frame -> True,
            FrameLabel -> {"MS / GeV", "Mh / GeV"}];

Export["MSSMEFTHiggs_Mh_MS.png", plot, ImageSize -> 600];
~~~~~~~~~~~~~~~~~~~~~~~

When this script is executed, the following figure is produced:

\image html MSSMEFTHiggs_Mh_MS.png

*/
